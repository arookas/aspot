
//
// This file is part of ASPOT.
// See README and LICENSE for details.
//

// -------------------------------------------------------------------------- //
// Base class for all indicator styles. Derive from this to create advanced
// styles and logic. See ASpotBasicStyle to quickly create simple styles.

class ASpotStyle abstract {

  //
  // [optional]
  // Used by the system when initializing the style. If the style needs to
  // cache any resource references or perform calculations, this is the place
  // to do it. The return value indicates success (true) or failure (false);
  // if the style fails to load, selecting it in the options will be fallback
  // to the builtin "ring" style. The default implementation does nothing.
  //
  virtual bool LoadStyle() {
    return true;
  }

  //
  // [optional]
  // Gives the name to this style. Used to identify this style by the
  // `aspot_style` CVAR. By default, the class name is used. It's best
  // to come up with a unique name to help prevent multiple styles with
  // conflicting names.
  //
  virtual string GetStyleName() const {
    return self.GetClassName();
  }

  //
  // Called when the spot is triggered. This function spawns the actor used
  // to track the location and duration of the spot. When the actor returned
  // by this function is destroyed, the indicator will go away. This actor is
  // also destroyed automatically when the associated player disconnects.
  //
  // Parameters:
  //       spot - active spot control structure. doesn't contain much useful
  //              info for this function yet but is reserved for future
  //              extensions.
  //     player - the player who is sending the ping.
  //      trace - data from the line trace.
  //        mob - the actor that will tracked with this ping. may be null.
  //       type - the spot/ping type. indicates enemy, ally, item, etc.
  //
  abstract play Actor SpawnSpot(
    in ASpotSpotData spot,
    PlayerPawn player,
    in FLineTraceData trace,
    Actor mob,
    ASpotType type
  );

  abstract ui void DrawSpot(
    in ASpotSpotData spot,
    PlayerPawn player,
    in ASpotScreen screen,
    ASpotCamera camera,
    RenderEvent e
  );

  protected static vector2 Calc2DPos(
    in ASpotScreen ascreen,
    ASpotCamera acamera,
    vector2 margin = (0, 0)
  ) {
    vector2 surface = (Screen.GetWidth(), Screen.GetHeight());
    vector2 pos = ascreen.SceneToWindow(acamera.ProjectToNormal());

    if (!acamera.IsInFront()) {
      vector2 ipos = (surface - pos);
      vector2 mid = (surface * 0.5);
      vector2 unit = (ipos - mid).Unit();

      pos = (mid + unit * max(surface.X, surface.Y));
    }

    if (pos.X < margin.X) {
      pos.X = margin.X;
    }

    if (pos.Y < margin.Y) {
      pos.Y = margin.Y;
    }

    if (pos.X > surface.X - margin.X) {
      pos.X = (surface.X - margin.X);
    }

    if (pos.Y > surface.Y - margin.Y) {
      pos.Y = (surface.Y - margin.Y);
    }

    return pos;
  }

}

// -------------------------------------------------------------------------- //
// Basic style providing an 8-second indicator with no sounds.
// This class can be derived from to make single-texture indicator very
// quickly.

class ASpotBasicStyle : ASpotStyle abstract {

  //
  // INTERFACE
  //

  //
  // Tells the style what texture to use for a given indicator type.
  // Used by LoadStyle() to load the resources for this indicator.
  // Multiple spot types may share textures. The texture will be drawn
  // at the image's origin, so make sure to set the image's X/Y offsets
  // to center the indicator.
  //
  protected abstract string GetTexName(ASpotType type) const;

  //
  // [optional]
  // Tells the style what actor to spawn to control indicator duration
  // and 3D effects. By default, 'ASpotBasicPuff' will stick around for
  // 8 seconds. The ping/spot goes away when this actor is destroyed.
  // You can use e.g. TNT1A0 to make the indicator 2D-only with no 3D.
  //
  protected virtual name GetSpotClass() const {
    return 'ASpotBasicPuff';
  }

  //
  // [optional]
  // Specifies the sound name to play when spotting. This may be blank to
  // indicate no sound. If using a custom puff class, you can also control
  // sounds using that actor instead and leave this blank. If non-empty,
  // the sound name should match the logical name as defined in SNDINFO.
  //
  protected virtual string GetSpotSound() const {
    return "";
  }

  //
  // [optional]
  // Controls how much space off the edge of the screen beyond which the
  // indicator is clamped, measured in pixels (i.e. same units as Screen).
  //
  protected virtual vector2 GetDrawMargin() const {
    return (0, 0);
  }

  //
  // IMPLEMENTATION
  //

  protected TextureID mTex[ASPOT_NUM_TYPES];

  override bool LoadStyle() {
    for (int i = 0; i < ASPOT_NUM_TYPES; ++i) {
      string texname = GetTexName(i);
      mTex[i] = TexMan.CheckForTexture(texname, TexMan.Type_Any);

      if (!mTex[i].isValid()) {
        Console.Printf("aspot: missing graphic '%s'", texname);
        return false;
      }
    }

    return true;
  }

  override Actor SpawnSpot(
    in ASpotSpotData spot,
    PlayerPawn player,
    in FLineTraceData trace,
    Actor mob,
    ASpotType type
  ) {
    Actor puff = Actor.Spawn(GetSpotClass(), trace.HitLocation);

    if (puff != null) {
      string soundname = GetSpotSound();

      if (soundname != "") {
        puff.A_StartSound(soundname,
          slot: CHAN_AUTO, flags: (CHANF_NOPAUSE | CHANF_UI | CHANF_OVERLAP)
        );
      }
    }

    return puff;
  }

  override void DrawSpot(
    in ASpotSpotData spot,
    PlayerPawn player,
    in ASpotScreen ascreen,
    ASpotCamera acamera,
    RenderEvent e
  ) {
    Actor mo = spot.GetActor();

    if (mo == null) {
      return;
    }

    acamera.ProjectActorPosPortal(
      mo, (0, 0, (mo.height / 2)), e.FracTic
    );

    TextureID tex = mTex[spot.type];
    vector2 pos = Calc2DPos(ascreen, acamera, GetDrawMargin());
    Screen.DrawTexture(tex, true, pos.X, pos.Y);
  }

}

class ASpotBasicPuff : Actor {

  Default {

    +NOINTERACTION
    +NOGRAVITY
    +NOBLOCKMAP

    Radius 1;
    Height 1;

  }

  States {

    Spawn:
      TNT1 AAAAAAAA 35; // 8 seconds
      Stop;

  }

}

// -------------------------------------------------------------------------- //
